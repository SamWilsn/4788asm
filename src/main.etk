%macro push0()
        # TODO: replace with push0 when it's added
        push1 0
%end

%def rootmod()
        98304
%end

%macro get_input()
        %push0()
        calldataload
%end

%macro get_timestamp_index()
        push3 rootmod()
        timestamp
        mod
%end
        
# If caller is from system, jump to lbl_update.
caller
push20 0xfffffffffffffffffffffffffffffffffffffffe
eq
push1 lbl_update
jumpi

########
# READ #
########

# Check if calldata is less than 32 bytes
push1 32        # [32]
calldatasize    # [calldatasize, 32]
eq              # [calldatasize == 32]
push1 load      # [revert_addr, calldatasize == 32]
# Jump to continue if length-check passed, otherwise revert
jumpi           # []
%push0()
%push0()
revert


load: 
# Load stored timestamp.
push3 rootmod() # [hrm]
%get_input()    # [time, hrm]
mod             # [time_index]
dup1            # [time_index, time_index]
sload           # [want, time_index]

# Verify stored timestamp matches input.
%get_input()    # [got, want, time_index]
eq              # [got == want, time_index]
iszero          # [got != want, time_index]
push1 exit      # [revert_addr, got != want, time_index]
jumpi           # [time_index]

# Extend index to get root index.
push3 rootmod() # [hrm, time_index]
add             # [root_index]
sload           # [root]

%push0()        # [0, root]
mstore          # []

exit: 
push1 32        # [size]
%push0()        # [offset]
return

##########
# UPDATE #
##########

lbl_update:
jumpdest
timestamp               # [time]
%get_timestamp_index()  # [time_index, time]
sstore                  # []

%push0()                # [0]
calldataload            # [root]
%get_timestamp_index()  # [time_index, root]
push3 rootmod()         # [hrm, time_index, root]
add                     # [root_index, root]
sstore

stop
